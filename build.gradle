plugins {
    id 'fr.brouillard.oss.gradle.jgitver' version '0.3.1'
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven'


    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    group 'de.justsoftware.toolbox'

    dependencies {
        testImplementation 'org.mockito:mockito-core:1.10.19'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'
        testImplementation('org.testng:testng:6.9.9') {
            exclude group: 'junit'
        }
        testImplementation project(':just-java-test-toolbox')
    }

    compileJava {
        options.compilerArgs += ['-Xlint:unchecked']
    }

    tasks.withType(Test) {
        useTestNG()
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    tasks.withType(Jar) {
        doFirst {
            manifest {
                manifest.attributes 'X-GIT-SHA1': "$project.ext.git_sha1_full"
            }
        }
    }

    jgitver {
        mavenLike true
    }

    artifacts {
        archives sourceJar
    }

    uploadArchives {
        repositories {
            repositories.mavenDeployer {
                repository(url: jucoRepositoryThirdParty) {
                    authentication(userName: repositoryUser, password: repositoryPassword)
                }
                snapshotRepository(url: jucoRepositorySnapshots) {
                    authentication(userName: repositoryUser, password: repositoryPassword)
                }
            }
        }
    }

}
